
(* Proof of Bellman-Ford algorithm *)

theory Graph

  use export int.Int
  use export set.Fset

  type vertex
  constant vertices : set vertex
  constant edges: set (vertex, vertex)
  constant s : vertex
  function weight vertex vertex : int

  axiom s_in_graph: mem s vertices

  axiom edges_def:
    forall x y: vertex. mem (x, y) edges ->
    (mem x vertices /\ mem y vertices)

  inductive path (v1 v2: vertex) (n: int) =
    | path_empty:
        forall v: vertex. path v v 0
    | path_succ:
        forall v1 v2 v3: vertex, n: int. path v1 v2 n ->
        mem (v2, v3) edges -> path v1 v3 (n + (weight v2 v3))

  inductive reachable (v: vertex) (d: int) =
    | reachable_empty:
        forall v: vertex. reachable s 0
    | reachable_succ:
        forall v1 v2: vertex, d: int. reachable v1 d ->
        mem (v1, v2) edges -> reachable v2 (d + 1)

  predicate shortest_path (v1 v2: vertex) (n: int) =
    (path v1 v2 n) /\
    (forall m: int. m < n -> not (path v1 v2 m))

  predicate no_path (v1 v2: vertex) =
    forall n: int. not (path v1 v2 n)

  lemma reach_nonneg:
    forall v: vertex, d: int.
    mem v vertices -> reachable v d -> d >= 0

  lemma reach_source:
    forall v: vertex, n: int.
    mem v vertices -> reachable v 0 -> v = s

  lemma reach_next:
    forall v: vertex, d: int. reachable v (d + 1) ->
    exists u: vertex. mem (u, v) edges /\ reachable u d

  lemma reach_most_n:
    forall v: vertex, d: int. mem v vertices ->
    0 <= d < cardinal vertices ->
    not (reachable v d) -> no_path s v

  predicate negcycle =
    exists v: vertex. mem v vertices /\
    exists n d: int. n < 0 /\ path v v n

end

theory Distance

  use import int.Int

  type dist = Finite int | Infinite

  function (++) (x: dist) (y: dist) : dist =
    match x with
      | Infinite -> Infinite
      | Finite x ->
        match y with
          | Infinite -> Infinite
          | Finite y -> Finite (x + y)
        end
    end

  predicate (<<) (x y: dist) =
    match x with
      | Infinite -> false
      | Finite x ->
        match y with
          | Infinite -> true
          | Finite y -> x < y
        end
    end

  predicate (>>=) (x y: dist) =
    match x with
      | Infinite -> true
      | Finite x ->
        match y with
          | Infinite -> false
          | Finite y -> x >= y
        end
    end

  clone export comparison.MinMax with type t = dist, predicate ge = (>>=)

end

theory FSetEx

  use export set.Fset

  function take (s:set 'a) : 'a

  axiom take_def:
    forall x : set 'a. not (is_empty x) ->
    mem (take x) x

  lemma set_empty:
    forall a: set 'a. is_empty a <-> a = empty

  lemma set_union_exchange:
    forall a b: set 'a. union a b = union b a

  lemma set_inter_exchange:
    forall a b: set 'a. inter a b = inter b a

  lemma set_inter_choice:
    forall a b: set 'a, e: 'a. inter a b = empty ->
    mem e a -> not (mem e b)

  lemma set_preserve_union:
    forall a b: set 'a, e: 'a. mem e a ->
    union (remove e a) (add e b) = union a b

  lemma set_preserve_inter:
    forall a b: set 'a, e: 'a. mem e a /\ not (mem e b) ->
    inter (remove e a) (add e b) = inter a b

  lemma set_empty_union:
    forall a: set 'a. union a empty = a

  lemma set_empty_inter:
    forall a: set 'a. inter a empty = empty

end

(* a bag is simply a reference containing a finite set *)
module Bag

  use export FSetEx
  use export module ref.Ref

  type bag 'a = ref (set 'a)

  let is_empty (b: bag 'a) =
    {} is_empty !b { result=True <-> is_empty !b }

  (*
  val push (x: 'a) (b: bag 'a) :
    {} unit writes b { !b = add x (old !b) }
  *)

  let pop (b: bag 'a) =
    { not (is_empty !b) }
    let x = take !b in
    b := remove x !b;
    x
    { mem result (old !b) /\ !b = remove result (old !b) }

end

module BellmanFord

  use import int.MinMax
  use import map.Map
  use import module ref.Ref
  use import Graph
  use import Distance
  use module Bag

  type distmap = map vertex dist

  function initialize_single_source (s: vertex) : distmap =
    (const Infinite)[s <- Finite 0]

  predicate paths (m: distmap) (pass: int) (via: set (vertex, vertex)) =
    forall v: vertex. mem v vertices ->
    match m[v] with
      | Finite n -> path s v n
      | Infinite ->
        (forall d: int. 0 <= d < pass -> not (reachable v d)) /\
        (forall u: vertex. mem (u, v) via ->
        forall d: int. 0 <= d < pass -> not (reachable u d))
    end /\
    ((no_path s v) -> m[v] = Infinite)

  lemma path_non_reachable:
    forall v: vertex. mem v vertices ->
    forall d: int. 0 <= d < cardinal vertices -> not (reachable v d) ->
    no_path s v

  let relax (m: ref distmap) (u v: vertex) (pass: int)
            (via: set (vertex, vertex)) =
    { mem (u, v) edges /\ not (mem (u, v) via) /\ paths !m pass via }
    let n = !m[u] ++ Finite (weight u v) in
    if n << !m[v] then m := !m[v <- n]
    { paths !m pass (add (u, v) via) }

  exception NegativeCycle (* list vertex *)

  let bellman_ford () =
    { }
    let m = ref (initialize_single_source s) in
    assert { paths !m 1 empty };

    for i = 1 to cardinal vertices - 1 do
      invariant { paths !m i empty }

      let es = ref edges in
      let es_co = ref empty in
      while not (Bag.is_empty es) do
        invariant { paths !m i !es_co /\
          union !es !es_co = edges /\ inter !es !es_co = empty }
        variant { cardinal !es }

        let (u, v) = Bag.pop es in
        assert { not (mem (u, v) !es_co) };
        relax m u v i !es_co;
        es_co := add (u, v) !es_co
      done
    done;

    let e = ref edges in
    while not (Bag.is_empty e) do
      variant { cardinal !e }

      let (u, v) = Bag.pop e in

      if (get !m v) <<
        ((get !m u) ++ (Finite (weight u v))) then
        raise NegativeCycle
    done;

    assert { paths !m (cardinal vertices) empty };

    !m


    { forall v: vertex. mem v vertices ->
      match result[v] with
      | Infinite -> no_path s v
      | Finite n -> path s v n
      end }
    | NegativeCycle -> { }

end

(*
Local Variables:
compile-command: "why3ide bf.mlw"
End:
*)
